generate an xscale
```
search
  c = [#chart width height xmargin xrule-height ymargin points:[x]]
  xmin = min[value:x given: x]
  xmax = max[value:x given: x]
  log10 = floor[value:log[value:xmax - xmin]]
  trybase = pow[value:log10 by:10]
  base = if ((xmax - xmin) / trybase) < 2 then trybase / 10 else trybase
  start = floor[value:xmin / base]
  end = ceiling[value:xmax / base]
  tick = range[from:start + 1 to:end - 1]
  scale = (width - xmargin) / (end - start)
  x1 = (tick - start) * scale

bind
   c.xscale := scale / base
   c.xoff := start * base

bind @browser
   c.xrule :=
      [#g
         transform = "translate({{xmargin}}, {{height - ymargin}})"
         children:
           [#line x1:0 x2:width - xmargin y1:0 y2:0 stroke-width:0.2 stroke:"black"]
           [#line x1 x2:x1 y1:0 y2:xrule-height stroke-width:0.2 stroke:"black"]
           [#text x:x1 y:xrule-height text-anchor:"center" font-size:4 text:tick * base]]
```

generate a yscale - ideally we could manipulate a g transform for the actual
data instead of exporting offset and scape
```
search
  c = [#chart height ymargin xmargin points:[y]]
  ymin = min[value:y given: y]
  ymax = max[value:y given: y]
  log10 = floor[value:log[value:ymax - ymin]]
  trybase = pow[value:log10 by:10]
  base = if ((ymax - ymin) / trybase) < 2 then trybase / 10 else trybase
  start = floor[value:ymin / base]
  end = ceiling[value:ymax / base]
  tick = range[from:start + 1 to:end - 1]
  scale = (height - ymargin)/(end - start)
  y1 = (end - tick) * scale

bind
   c.yscale := scale / base
   c.yoff := start * base

bind @browser
   c.yrule :=
      [#g children:
       [#line x1:xmargin x2:xmargin y1:0 y2:height - ymargin stroke-width:0.2 stroke:"black"]
       [#line x1:xmargin - 10 x2:xmargin y1 y2:y1 stroke-width:0.2 stroke:"black"]
       [#text x:0 y:y1 text-anchor:"center" font-size:4 text:tick * base]]
```

draw a circle for each point in the data set
```
search
  c = [#chart width height xscale xmargin yscale ymargin yoff xoff points:[x y]]

bind @browser
  c.dots := [#g transform:"scale(1, -1) translate({{xmargin}}, {{ymargin - height}})"
                children:
                  [#circle r:0.6 cx:xscale * (x - xoff) cy:yscale * (y - yoff)]]
```

connect each of the adjacent dots by crossing two sorts and picking
out those pairs which are sequential
```
search (@browser @session)
  c = [#chart xoff yoff xscale yscale points:[x y] dots]
  order = sort[value: x]
  c = [#chart points:[x:x2 y:y2]]
  order2 = sort[value: x2]
  order2 = order + 1
  // should be multiple lines
  color = if c.linecolor then c.linecolor else "black"

bind @browser
  dots.children += [#line x1:xscale * (x - xoff) x2: xscale * (x2 - xoff)
                          y1: yscale * (y - yoff) y2 : yscale * (y2 - yoff)
                          stroke-width:0.2 stroke:color]
```

construct a horizontalflexbox container
```
search @browser
     d = [#row]
bind @browser
     d.tag := "div"
     d.style := [display:"flex"]
```

construct a centering flexbox container
```
search @browser
     d = [#centering style]
bind @browser
     d.tag := "div"
     style.display := "flex"
     style.justify-content := "center"
```


contruct a box flagged to grow to fill the available space
```
search @browser
     d = [#grow-box style]
bind @browser
     d.tag := "div"
     style.flex-grow := 1
```

build up a chart display in the browser by finding associated X and Y rules,
optional titles, and assemble with the titles using flex boxes
```
search (@browser @session)
   c = [#chart width height dots xrule yrule xmargin ymargin xrule-height]
   title = if c.title then c.title else ""
   x-title = if c.x-title then c.x-title else ""
   y-title = if c.x-title then c.y-title else ""
bind @browser
   s = [#svg viewBox: "0 0 {{width}} {{height}}" children:(xrule yrule dots)]
   y-label = [#div style:[color:"black" height:"1.2em" font-size:15 transform:"rotate(-90deg)"] text:y-title]
   [#row children:(
         [#grow-box sort:1 style:[]
                         children:[#centering style:[flex-direction: "column"]
                                   children:
                                     ([#centering style:[] children: [#div style:[color:"black" height:"1.2em" font-size:24] text:title]]
                                       s
                                       [#centering style:[] children:[#div style:[color:"black" height:"1.2em" font-size:15] text:x-title]])]]
         [#centering style:[flex-direction: "column"] children:y-label])]
```


buildchart
```
commit
    c = [#chart #memory-chart width:100 height:100 xmargin:15 ymargin:15 xrule-height:5]
```

remove earliest point if there are more than 60
```
search
    [#time hours minutes seconds]
    c = [#memory-chart points]
    npoints = count[given: points.x]
    npoints > 30
    1 = sort[value: points.x]
commit
    points := none
```


add a point every second
```
  search
    [#time frames]
    rss = sin[angle: frames]
    c = [#memory-chart]
  commit
    c.points += [x: frames y:rss]
```
