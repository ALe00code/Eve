# Chat

Chat is a fully functional multi-user chat client. The `@shared` DB is synced between all clients on the server, letting them pass messages to one another without explicit requests. Since the current version of Eve is running in local-only mode, this will be a bit of an echo chamber.

Add a user for this session when the program starts.

### Setup

```
commit
  [#user name: "me"]
```

### Drawing

Draw the chat pane.

```
search
  channel = "general"
bind @browser
  [#div class: "chat-pane" children:
    [#div #chat-messages class: "chat-messages" channel]
    [#input #channel-input autofocus: true channel]]
```

Draw messages for each channel into the appropriate chat log.

```
search @session @shared @browser
  parent = [#chat-messages channel]
  id = [#message name time message channel]
bind @browser
  parent.children += [#div id, class: "chat-message", children:
                       [#div class: "flex-row" children:
                         [#div class: "chat-user", text: name]
                         [#div text: "at" style: [margin: "0 6px"]]
                         [#div class: "chat-time", text: time]]
                       [#div class: "chat-message", text: message]]
```

### Events

Handle sending messages from the chat input.

```
  search @event @browser @session
    event = [#keydown element, key: "enter"]
    element = [#channel-input channel value]
    [#user name]
    [#time hours minutes seconds]
  commit @browser
    element.value := ""
  commit @shared
    [#message event name, time: "{{hours}}:{{minutes}}", message: value, channel]
```
