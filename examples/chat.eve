# Chat

add a user for this sesion on connect

```
  match
    [#session-connect]
  commit
    [#user @me]
```

build the chat pane

```
  match
    channel = "general"
  bind @browser
    [#div class: "chat-pane" children:
      [#div #chat-messages class: "chat-messages" channel]
      [#input #channel-input autofocus: true channel]]
```

draw messages

```
  match
    parent = [#chat-messages channel]
    id = [#message name time message channel]
  bind @browser
    parent.children += [#div id, class: "chat-message", children:
                         [#div class: "chat-user", text: name]
                         [#div class: "chat-time", text: time]
                         [#div class: "chat-message", text: message]]
```

handle chat keydowns

```
  match
    event = [#keydown element, key: "enter"]
    element = [#channel-input channel value]
    [#user name]
    [#time hours minutes seconds]
  commit @browser
    element.value := ""
  commit @all
    [#message event name, time: "{{hours}}:{{minutes}}", message: value, channel]
```
