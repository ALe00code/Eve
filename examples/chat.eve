# Chat

add a user for this sesion on connect

```
  commit
    [#user @me]
```

build the chat pane

```
  search
    channel = "general"
  bind
    [#div class: "chat-pane" children:
      [#div #chat-messages class: "chat-messages" channel]
      [#input #channel-input autofocus: true channel]]
```

draw messages

```
  search (@session, @shared)
    parent = [#chat-messages channel]
    id = [#message name time message channel]
  bind
    parent.children += [#div id, class: "chat-message", children:
                         [#div class: "chat-user", text: name]
                         [#div class: "chat-time", text: time]
                         [#div class: "chat-message", text: message]]
```

handle chat keydowns

```
  search
    event = [#keydown element, key: "enter"]
    element = [#channel-input channel value]
    [#user name]
    [#time hours minutes seconds]
  commit
    element.value := ""
  commit @shared
    [#message event name, time: "{{hours}}:{{minutes}}", message: value, channel]
```


remove events
~~~
  search
    c = if event = [#keydown] then event
        if event = [#keyup] then event
        if event = [#click] then event
        if event = [#double-click] then event
        if event = [#change] then event
        if event = [#blur] then event
        if event = [#focus] then event
  commit
    c := none
~~~

hash changes
~~~
  search
    change = [#url-change hash-segment]
  commit
    change := none
    url = [#url]
    url.hash-segment := hash-segment
~~~

update values
~~~
  search
    [#change element value]
  commit
    element.value := value
~~~

