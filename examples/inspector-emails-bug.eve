# Inspector Emails Bug

This example tests the inspector's ability to expose cardinality information.

Create some admins, clients, and a message.

```
commit
  [#client name: "Jen" email: "jen@example.com" | interest: "things"]
  [#client name: "Robert" email: "bobby@example.com" | interest: "stuff"]
  [#client name: "Susan" email: "sue@example.com" | interest: "news"]

  [#admin name: "Eric" email: "edawg@admin.example.com"]
  [#admin name: "Zach" email: "zbot@admin.example.com"]

commit @outbox
  [#message text: "Hope you're having a pleasant day, remember to buy our stuff!" | interest: ("things", "stuff")]
```

For each message in the `@outbox` db, send it to all the clients in its interest groups.

```
search @outbox
  [#message text interest]

search
  client = [#client interest]
  admin = [#admin]

commit @email
  [#message from: admin.email to: client.email text:
  "Hey {{client.name}}!
  We know you're interested in {{interest}}, so we're sending you this message:
  {{text}}"]
```

Sanity check to make sure we're not spamming our clients.

```
search @email
  message = [#message]
  actual = count[given: message]

search @outbox
  template = [#message interest]

search
  client = [#client interest]
  expected = count[given: (template, client)]
  not(expected = actual)

bind @browser
  [#div sort: 1 style: [margin-bottom: 10 font-weight: "bold" color: "red"] text: "We've sent {{actual}} emails, but were supposed to send {{expected}}!"]
```

Draw the emails we've sent.

```
search @email
  message = [#message to text]

bind @browser
  [#div sort: 2 style: [border-top: "1px solid #A0A0A0"] children:
    [#div message style: [padding-bottom: 10 margin-top: 10 border-bottom: "1px solid #A0A0A0"] children:
      [#div style: [margin-bottom: 10] text: "To {{to}}"]
      [#div text]]]
```
