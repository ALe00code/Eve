# Handle Events
click on a restaurant or pin, show a restaurant
  match
    app = [@app]
    restaurant = if [#click element: [#yelp-restaurant-list-element restaurant]] then restaurant
                else if [#click element: [#yelp-restaurant-pin restaurant]] then restaurant
  commit
    app.selected := restaurant
    app.content := "restaurant"

click on yelp logo, show map
  match
    app = [@app]
    [#click element: [#yelp-logo]]
  commit
    app.content := "map"

# Draw the page
draw the selected restaurant
  match
    root = [@yelp-root]
    restaurant = [#restaurant name image]
    [@app content: "restaurant", selected: restaurant]
  bind
    root.children += [#div class: "restaurant-info", children:
                        [#div class: "info-header", children:
                          [#h2 text: name]
                          [#div children:
                            [#img src: image]]]]

draw the map pane
  match
    root = [@yelp-root]
    [@app content: "map"]
    [#restaurant street city state zip]
    [#address-to-latlon street city state zip lat long]
  commit
    root.children += [#map class: "map", pins: [#yelp-restaurant-pin lat lon]]

draw the restaurant list
  match
    restaurant = [#restaurant name rating]
    root = [@yelp-root]
    star-image = "star{{rating}}.png"
  bind
    root.children += [#div class: "restaurant-list-container" children:
                      [#div class: "restaurant-list-header" children: [#h1 text: "Restaurants"]]
                      [#div class: "restaurant-list-elements" children:
                        [#div #yelp-restaurant-list-element, class: "restaurant-list-element",
                          restaurant, name, star-image, children:
                          [#h1 text: name]
                          [#img src: star-image]]]]

draw the main page
  bind
    [#div @yelp-root class: "yelp-root", children:
      [#div class: "header", children: [#h1 #yelp-logo text: "Yelp"]]]
