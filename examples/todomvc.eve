handle insert
  [#keydown element: [@new-todo value] key: "enter"]
  save
    [#todo body: value, editing: false, completed: false]

handle edit
  (todo, body, editing, completed) =
    if [#click element: [#todo-checkbox todo]] then (todo, todo.body, todo.editing, toggle(todo.completed))
    else if [#click element: [@toggle-all checked]] then ([#todo body], body, todo.editing, toggle(checked))
    else if [#double-click element: [#todo-item todo]] then (todo, todo.body, true, todo.completed)
    else if [#blur element: [#todo-editor todo value]] then (todo, value, false, todo.completed)
    else if [#keydown element: [#todo-editor todo] key: "escape"] then (todo, todo.body, false, todo.completed)
    else if [#keydown element: [#todo-editor todo value] key: "enter"] then (todo, value, false, todo.completed)

  save
    todo := [body, completed, editing]

handle removes
  todo = if [#click element: [#remove-todo todo]] then todo
         else if [#click element: [@clear-completed]] then [#todo completed: true]
  save
    todo -= [#todo]

draw the children
  filter = "all"
  parent = [@todo-list]
  todo = [#todo, body, editing, completed]

  maintain
    parent.children += [#li, class: [todo: true, completed, editing], todo, children:
                         [#input #todo-checkbox, todo, class: "toggle", type: "checkbox", checked: completed]
                         [#label #todo-item, todo, text: body, style: [display: toggle(editing)]]
                         [#input #todo-editor, todo, value: body, autofocus: true, style: [display: editing]]
                         [#button #remove-todo todo text: "X"]]

draw todomvc
  filter = "all"
  all-checked = if not([#todo completed: false]) then true
                else false
  count = if c = count(given [#todo completed: false]) then c
          else 0
  hide-clear-completed = is(count > 0)
  maintain
    [#div @todoapp children:
      [#header children:
        [#h1 text: "todos"]
        [#input @new-todo, autofocus: true, placeholder: "What needs to be done?"]]
      [#div children:
        [#input @toggle-all, type: "checkbox", checked: all-checked]
        [#ul @todo-list]]
     [#footer children:
        [#span @todo-count children: [#strong text: count] [#span text: " items left"]]
        [#button @clear-completed, style: [display: hide-clear-completed] text: "Clear completed"]]]
